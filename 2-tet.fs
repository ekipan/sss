\ \ TETRIS FOR DUREXFORTH 4.
\ NUMBERED 6->1 FOR COMPILE COUNTDOWN.
\ IF YOU'RE DETERMINED TO READ THIS,
\ START W/ THE BIG SECTION 5 COMMENT.

DECIMAL MARKER --TET--

10 VALUE #10  4 VALUE 4         \ ARITH
: 40- 40 - ;  : >10+> SWAP #10 + SWAP ;
: 4* 2* 2* ;  : 10* DUP 4* + 2* ;
: 0* DROP 0 ; : 40* 10* 4* ;

6 . \ TOOLS AND NONPORTABLE.

: H. ( U-) HEX U. DECIMAL ;  \ DEVTOOLS
: REDO ( -) --TET-- S" TET.FS"
  INCLUDED ( MUST TCO! SAFE W/ DF. ) ;
CREATE BX  $D020 EOR, $D020 STA, RTS,
: PROFILE ( COLOR -- ) HERE >R  DUP
  LDA,# BX JSR, LATEST >XT JSR, LDA,#
  BX JMP,  R> LATEST NAME>STRING + ! ;
: PROF ( ENABLE-TIME-PROFILING? -- )
  IF $4D ELSE $60 THEN BX C! ;
\ : PROF DROP ; : PROFILE DROP ;

: ERASE ( AU-) 0 FILL ;          \ LANG
: ;THEN  POSTPONE EXIT POSTPONE THEN ;
IMMEDIATE
: RDROP ( R:A-) PLA, PLA, ; IMMEDIATE
: SPLIT ( $YYXX -- $XX $YY ) [ 0 LDY,#
  MSB LDA,X MSB STY,X ] PUSHYA ;

: SYNC ( -) [ 213 LDA,# $D012 CMP, \ HW
  -5 BNE, ] ;  6 PROFILE
: KBINIT ( -) $B80 $28A ! 0 $C6 C! ;
: KBPOLL ( -C; W/ FAST REPEAT HACK.)
  KEY? IF 1 $28B C! KEY ;THEN 0 ;
: ENTROPY ( -U) $A1 @ DUP 0= + ;

13 22 ( COL ROW ) 40* + DUP    \ SCREEN
$D800 + CONSTANT COLORMEM
$0400 + CONSTANT TILEMEM
: BG ( -) 0 $D020 ( BLACK BG+BORDER ) !
  11 $286 ( GRAY FG ) C! PAGE TILEMEM
  38 ( NEXTROW-2 ) + 21 ( ROWS ) 0 DO
  DUP 19 $A0 ( RVSPACE ) FILL 40- LOOP
  ( TOP ) 2+ #10 $A0 FILL ;
: PAINT ( AA-) COLORMEM BEGIN
  2DUP #10 MOVE >10+> 40-
  OVER 3 PICK = UNTIL  DROP 2DROP ;
: TH-C ( P-A) SPLIT 40* - COLORMEM + ;
: P! ( PC-C) DUP ROT TH-C C! ;
: PLOT ( PPPPC-) P! P! P! P! DROP ;
: RUB ( P-) TH-C 2 - DUP 4 ERASE 40-
  4 ERASE ;

\ W = ZP TEMP, LSB/MSB,X = ZP STACK.
\ P@ SCAN TABLE, ADD CENTER (P)OSITION.
: W! ( A-) [ LSB LDY,X W STY, MSB LDY,X
  W 1+ STY, INX, 0 LDY,# ] ;
: P@ ( P-PP) DUP [ CLC, W LDA,(Y) INY,
  LSB 1+ DUP ADC,X STA,X W LDA,(Y) INY,
  MSB 1+ DUP ADC,X STA,X ] ;

5 . \ DATA, PIECE DEFINITION.

\ (') MEANS PARSE, (*) MEANS VARYING.
: N: ( *'-*) PARSE-NAME EVALUATE ;
: C: ( U'-) 0 DO N: C, LOOP ;
: >P ( C-P) DUP 4* 4* OR $F0F AND 2 - ;
: P:  HEX 8 0 DO N: >P , LOOP DECIMAL ;

CREATE COLORS 7 C: 3 8 6 4 5 2 7
CREATE BLOCKS \ CENTER (C/.) AT YX=02:
P: 00 01 02 03  02 12 22 32  \ IICI
P: 00 01 02 03  02 12 22 32
P:  03 11 12 13  01 02 12 22 \    JJJ
P:  01 02 03 11  02 12 22 23 \     .J
P: 01 11 12 13  02 12 22 21  \ LLL
P: 01 02 03 13  03 02 12 22  \ L.
P:  02 11 12 13  02 11 12 22 \    TTT
P:  01 02 03 12  02 12 13 22 \     C
P: 01 02 12 13  02 11 12 21  \  SS
P: 01 02 12 13  02 11 12 21  \ SC
P:  03 02 11 12  02 12 13 23 \    ZZ
P:  03 02 11 12  02 12 13 23 \     CZ
P: 01 02 11 12  01 02 11 12  \ OO
P: 01 02 11 12  01 02 11 12  \ OC
\ 7 SHAPES 4 TURNS 4 BLOCKS 2 BYTES.
: PIECE ( PTS-PPPPC) DUP >R 4* + 4* 2*
  BLOCKS + W! P@ P@ P@ P@ DROP R>
  COLORS + C@ ;  14 PROFILE

\ A PIECE IS: CENTER (P)OSITION HEX
\ $YYXX FROM BOTTOM LEFT, CLOCKWISE
\ (T)URNS COUNT 0-3, AND (S)HAPE INDEX
\ 0-6 IJLTSZO. STORED TO GAMESTATE:
\   ENQUEUE (S-) ->TAIL, HEAD->PLAYER.
\   ENTER (-) ROW 19 COL 5 TURNS 0.
\   CURR+! (PT-) MOVE/ROTATE PLAYER.
\   HOLD (-) SWAP S AND RESERVE S.
\   >OLD (-) REMEMBER DRAWN PIECE.
\ FETCHED OUT OF GAMESTATE:
\   OLD@ (-PTS) TO ERASE FROM SCREEN.
\   CURR (-PTS) IN PLAY.
\   CURR+ (PT-PTS) TO CHECK A MOVE.
\ THEN BLOCK POSITIONS COMPUTED:
\   PIECE (PTS-PPPPC) AND (C)OLOR.
\   HIT? (PPPPC-F) CHECK PLAYFIELD.
\   LOCK (PPPPC-) STORE TO PLAYFIELD.
\   PLOT (PPPPC-) STORE TO SCREEN.

CREATE FRAMES 5 C: 33 25 21 17 15
11 C: 13 12 10 8 7 6 5 4 3 3 2
: GRAV ( U-U) 15 MIN FRAMES + C@ ;

4 . \ CORE: VARS, INDEX, FETCH, STORE.

: VAR+ ( AU'-A) OVER VALUE + ;
$CC00 \ GLOBAL GAME VARIABLES:
210 VAR+ WELL \ 21X10 VISIBLE PLAYAREA.
20 VAR+ SPILL \ 2 ROWS ABOVE SCREEN.
0 VAR+ ROOF   \ ADDRESS AFTER. VALUES:
\ 0 EMPTY, 1 MARKED, 2-8 BLOCK COLORS.
2 VAR+ POS   \ $YYXX FROM BOTTOM LEFT.
1 VAR+ TURNS \ 0-3 CLOCKWISE.
1 VAR+ SHAPE \ 0-6 IJLTSZO.
1 VAR+ HELD  \ 0-6 WITH PIN BIT $08.
4 VAR+ QUEUE \ 0-6 NEXT RANDOM SHAPES.
2 VAR+ QIDX  \ QUEUE HEAD, USED MOD 4.
2 VAR+ SEED  \ FOR RANDOM GENERATOR.
2 VAR+ LINES \ FOR GRAVITY CURVE.
2 VAR+ %GRAV \ N->0 FALL TIMER.
2 VAR+ %STOP \ N->0 LINE SWEEP TIMER.
1 VAR+ SIG   \ 99 IF INITIALIZED.
WELL - CONSTANT SIZE

: PINNED? ( -F) HELD C@ 8 AND ;
: HELD@ ( -S) HELD C@ 7 AND ;
: HELD! ( S-) HELD C! ;
: UNPIN ( -) HELD@ HELD! ;
: HOLD ( -) HELD@  SHAPE C@ 8 OR HELD!
  SHAPE C! ;

: TH-W ( P-A) SPLIT 10* + WELL + ;
: ROW ( -A) POS 1+ C@ 10* WELL + ;
: CURR ( -PTS) POS @ TURNS @ SPLIT ;
: T@+ ( T-T) TURNS C@ + 3 AND ;
: CURR+ ( PT-PTS) SWAP POS @ +
  SWAP T@+  SHAPE C@ ;
: CURR+! ( PT-) T@+ TURNS C!  POS +! ;
: ENTER ( -) $1305 POS ! 0 TURNS C! ;

: TH-Q ( I-A) QIDX C@ + 3 AND QUEUE + ;
: ENQUEUE ( S-) 1 QIDX +!  3 TH-Q C!
  0 TH-Q C@ SHAPE C! ;

: ROLL ( U-U; 0 <= U2 < U1.) SEED @
  31421 * 6927 + DUP SEED !  UM* NIP ;
: INIT ( U-) WELL SIZE ERASE  SEED !
  4 ENQUEUE 5 ENQUEUE 4 ENQUEUE 4 ROLL
  ENQUEUE  5 HELD!  ENTER  99 SIG C! ;

3 . \ DRAW, WITH DIRTY BITSET.

\ DRAW BITS D?      \ EVENT BITSETS D!
$01 CONSTANT #DEL   $03 CONSTANT #GO
$02 CONSTANT #CURR  $06 CONSTANT #NEXT
$04 CONSTANT #QUEUE $0B CONSTANT #HOLD
$08 CONSTANT #HELD  $1E CONSTANT #ALL
$10 CONSTANT #WELL
VARIABLE DIRTY
: D? ( U-F) DIRTY @ AND ;
: D! ( U-) DIRTY @ OR DIRTY ! ;

VARIABLE OLD 0 ,
: OLD@ ( -PTS) OLD @ OLD 2+ @ SPLIT ;
: >OLD ( -) POS OLD 4 MOVE ;

\ 6: RUB (P-) PLOT (PPPPC-) PAINT (AA-)
: SLOT ( SP) DUP RUB 0 ROT PIECE PLOT ;
: Q ( IP-IP) OVER TH-Q C@ OVER SLOT
  SWAP 1+ SWAP $300 - ;
: DRAW ( -) SYNC
  #WELL D? IF SPILL WELL PAINT THEN
  #DEL D? IF OLD@ PIECE 0* PLOT THEN
  #CURR D? IF CURR PIECE PLOT >OLD THEN
  #QUEUE D? IF 1 $110D Q Q Q 2DROP THEN
  #HELD D? IF HELD@ $050D SLOT THEN
  0 DIRTY ! ;  6 PROFILE

2 . \ RULES: QUEUE, WELL, PLAYER.

\ TGMLIKE REROLLER RNG. 4: ROLL (U-U).
: Q? ( SI-S/SI-) TH-Q C@ OVER =
  IF DROP RDROP THEN ;
: QN ( -) 7 ROLL 0 Q? 1 Q? 2 Q? 3 Q?
  ENQUEUE R> RDROP >R ;  12 PROFILE
: QNEXT ( -) QN QN QN 7 ROLL ENQUEUE ;
: INIT ( U-) INIT QNEXT QNEXT QNEXT ;

\ COUNT, WHITEN, DELETE ROWS (A).
: FULL? ( A-F) DUP >10+> BEGIN  DUP C@
  WHILE  1+ 2DUP = UNTIL THEN  = ;
: M+ ( AU-AU) OVER FULL? IF  1+ OVER
  #10 1 FILL THEN  >10+> ;  7 PROFILE
: MARK ( A-U) 0 M+ M+ M+ M+ NIP ;
: S+ ( AA-AA) OVER C@ 1- IF  2DUP #10
  MOVE #10 + THEN  >10+> ;  7 PROFILE
: SWEEP ( -) WELL WELL BEGIN  S+ OVER
  ROOF = UNTIL  NIP ROOF OVER - ERASE ;

\ CHECK, STORE INTO WELL.
\ REUSE OOB P AS NONZERO F:
: H? ( PF-F) SWAP DUP  SPLIT 23 U<
  SWAP #10 U< AND  IF TH-W C@ THEN OR ;
: HIT? ( PPPPC-F) 0* H? H? H? H? ;
1 PROFILE
: L! ( PC-C) DUP ROT TH-W C! ;
: LOCK ( PPPPC-) L! L! L! L! DROP ;

\ MOVE. KICK BIAS CCW>L CW>R. F HIT?
$-100 CONSTANT DOWN
: GO ( PT-F) 2DUP CURR+ PIECE HIT?
  IF 2DROP 1 ;THEN  CURR+! #GO D! 0 ;
: TK ( PT-) GO 0= IF RDROP RDROP THEN ;
: TURNKICK ( T-) >R  0 R@ TK  R@ R@ TK
  0 R@ - R@ TK  DOWN R@ TK  DOWN R@ +
  R@ TK  DOWN R@ - R@ TK  RDROP ;
: LAND ( -F) KBINIT  CURR PIECE LOCK
  ROW MARK ?DUP IF  LINES +! 12 %STOP !
  #WELL ELSE #NEXT THEN D!  QNEXT ENTER
  UNPIN  CURR PIECE HIT? ;
: FALL ( -F) DOWN 0 GO IF LAND ELSE 0
  THEN LINES @ 3 RSHIFT GRAV %GRAV ! ;
: TRYHOLD ( -) PINNED? IF ;THEN
  HOLD ENTER  #HOLD D! ;

1 . \ MAIN: TIMERS, INPUT.

: HELP ( -) CR ." - GAME PAUSED -"
CR ." ENTER [NEW] OR [R]ESUME TO PLAY."
CR ." [SDF] MOVE [JK] ROTATE [L] HOLD."
CR ." ANY OTHER KEY TO PAUSE. " CR ;

: TICK ( A-F) -1 OVER +! C@ 0= ;
: STEP ( -- GAMEOVER? )
  %STOP C@ IF %STOP TICK
    IF SWEEP #ALL D! THEN 0 ;THEN
  %GRAV TICK IF FALL ;THEN
  KBPOLL 0 OF 0 ;THEN
  'D' OF FALL ;THEN
  'S' OF -1 0 GO 0* ;THEN
  'F' OF 1 0 GO 0* ;THEN
  'J' OF -1 TURNKICK 0 ;THEN
  'K' OF 1 TURNKICK 0 ;THEN
  'L' OF TRYHOLD 0 ;THEN
  PAGE HELP ;  11 PROFILE

: R ( -) 99 SIG C@ <> IF ;THEN KBINIT
  BG #ALL D! BEGIN DRAW STEP UNTIL ;
: NEW ( -) ENTROPY INIT R ;
' HELP START !  0 PROF

.( WORDS: HELP NEW R  )

: DD ( -) #ALL D! DRAW ;
: SS ( -) WELL $CE00 SIZE MOVE ;
: LL ( -) $CE00 WELL SIZE MOVE ;
